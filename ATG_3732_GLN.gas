Program.Sub.ScreenSU.Start
gui.F_GLN..create
gui.F_GLN..caption("GLN")
gui.F_GLN..size(2520,1560)
gui.F_GLN..position(0,0)
gui.F_GLN..event(unload,Unload)
gui.F_GLN..alwaysontop(False)
gui.F_GLN..fontname("Arial")
gui.F_GLN..fontsize(8)
gui.F_GLN..forecolor(0)
gui.F_GLN..fontstyle(,,,,)
gui.F_GLN..BackColor(-2147483633)
gui.F_GLN..controlbox(True)
gui.F_GLN..maxbutton(False)
gui.F_GLN..minbutton(True)
gui.F_GLN..mousepointer(0)
gui.F_GLN..moveable(True)
gui.F_GLN..sizeable(False)
gui.F_GLN..ShowInTaskBar(True)
gui.F_GLN..titlebar(True)
gui.F_GLN.txtGLN.create(textbox,"",True,2000,300,0,200,200,True,0,Arial,8,-2147483643,1)
gui.F_GLN.txtGLN.event(keypressenter,cmdsave_click)
gui.F_GLN.txtGLN.maxLength(16)
gui.F_GLN.cmdSave.create(button)
gui.F_GLN.cmdSave.caption("Save")
gui.F_GLN.cmdSave.visible(True)
gui.F_GLN.cmdSave.size(855,375)
gui.F_GLN.cmdSave.zorder(0)
gui.F_GLN.cmdSave.position(200,600)
gui.F_GLN.cmdSave.enabled(True)
gui.F_GLN.cmdSave.fontname("Arial")
gui.F_GLN.cmdSave.fontsize(8)
gui.F_GLN.cmdSave.event(click,cmdsave_click)


Program.Sub.ScreenSU.End

Program.Sub.Preflight.Start

Variable.Global.sCust.Declare(String)
Variable.Global.sShipTo.Declare(String)
Program.Sub.Preflight.End

Program.Sub.Main.Start
'Coded by: SMC
'Project Start Date: 1/10/2011
'Hooks:
'  ARS002A1-POPULATE-HOOK (MAIN CUSTOMER MAINT SCREEN) (000014350)
'  ARS002A1-SCRIPT-2-HOOK (MAIN CUSTOMER MAINT SCREEN) (000014370)
'  ARS002S-POPULATE-HOOK (ADDL CUST SHIP-TO MAINT) (000014410)
'  ARS002S-SCRIPT-2-HOOK (ADDL CUST SHIP-TO MAINT) (000014430)
'Notes:
'  Quote 2018 For Customer Buffalo Filter
'
'  Script 2 on both the Customer Master and Add'l Ship To Addresses screen will now allow users to enter a Global Location Numbr (GLN).
'
'  If no GLN has been entered, the Script 2 button will read "GLN REQUIRED".  If a GLN HAS been entered, it will read "GLN".  Clicking on the Script 2 button will pop up an input box, which will show the current GLN if it has been entered.
'  The users may enter a GLN that is up to 16 characters long.  Clicking OK on the input box will update the GLN and what the Script 2 button reads (GLN or GLN REQUIRED).

F.Intrinsic.Debug.ShowCallerInfo
F.ODBC.Connection!conx.OpenConnection(V.Ambient.PDSN,V.Ambient.PUser,V.Ambient.PPass)

F.Intrinsic.Control.If(V.Caller.Hook,=,14350)
	V.Global.sCust.Set(V.Passed.000002)
	V.Global.sShipTo.Set("")
	F.Intrinsic.Control.CallSub(Script2_name)
	V.Passed.000202.Set(V.Args.Script2)
	F.Intrinsic.Control.CallSub(Unload)
F.Intrinsic.Control.ElseIf(V.Caller.Hook,=,14410)
	V.Global.sCust.Set(V.Passed.000001)
	V.Global.sShipTo.Set(V.Passed.000003)
	F.Intrinsic.Control.CallSub(Script2_name)
	V.Passed.000202.Set(V.Args.Script2)
	F.Intrinsic.Control.CallSub(Unload)
F.Intrinsic.Control.ElseIf(V.Caller.Hook,=,14370)
	V.Global.sCust.Set(V.Passed.000002)
	V.Global.sShipTo.Set("")
	F.Intrinsic.Control.CallSub(Gln)
F.Intrinsic.Control.ElseIf(V.Caller.Hook,=,14430)
	V.Global.sCust.Set(V.Passed.000001)
	V.Global.sShipTo.Set(V.Passed.000003)
	F.Intrinsic.Control.CallSub(Gln,"Cust",V.Passed.000002,"ShipTo","")
F.Intrinsic.Control.Else
	F.Intrinsic.Control.CallSub(Unload)
F.Intrinsic.Control.EndIf

Program.Sub.Main.End

Program.Sub.Script2_Name.Start
V.Local.sQuery.Declare(String)
V.Local.sScript2.Declare(String)

F.Intrinsic.String.Concat("SELECT * FROM ATG_3732_GLN WHERE CUSTOMER='",V.Global.sCust,"' AND SHIPTO='",V.Global.sShipTo,"'",V.Local.sQuery)
F.ODBC.Connection!conx.OpenRecordsetRO("rstGLN",V.Local.sQuery)
F.Intrinsic.Control.If(V.ODBC.conx!rstGLN.EOF,<>,True)
	V.Local.sScript2.Set("GLN")
F.Intrinsic.Control.Else
	V.Local.sScript2.Set("Enter GLN")
F.Intrinsic.Control.EndIf
F.ODBC.conx!rstGLN.Close

F.Intrinsic.Variable.AddRV("Script2",V.Local.sScript2)

Program.Sub.Script2_Name.End

Program.Sub.Unload.Start
F.ODBC.Connection!conx.Close
F.Intrinsic.Control.End

Program.Sub.Unload.End

Program.Sub.GLN.Start
V.Local.sQuery.Declare(String)

F.Intrinsic.String.Concat("SELECT * FROM ATG_3732_GLN WHERE CUSTOMER='",V.Global.sCust,"' AND SHIPTO='",V.Global.sShipTo,"'",V.Local.sQuery)
F.ODBC.Connection!conx.OpenRecordsetRO("rstGLN",V.Local.sQuery)
F.Intrinsic.Control.If(V.ODBC.conx!rstGLN.EOF,<>,True)
	Gui.F_GLN.txtGLN.Text(V.ODBC.conx!rstGLN.FieldValTrim!GLN)
F.Intrinsic.Control.EndIf
F.ODBC.conx!rstGLN.Close

Gui.F_GLN..Show

Program.Sub.GLN.End

program.sub.cmdsave_click.start
V.Local.sQuery.Declare(String)

F.Intrinsic.String.Concat("SELECT * FROM ATG_3732_GLN WHERE CUSTOMER='",V.Global.sCust,"' AND SHIPTO='",V.Global.sShipTo,"'",V.Local.sQuery)
F.ODBC.Connection!conx.OpenRecordsetRW("rstGLN",V.Local.sQuery)
F.Intrinsic.Control.If(V.ODBC.conx!rstGLN.EOF,=,True)
	F.ODBC.conx!rstGLN.AddNew
	F.ODBC.conx!rstGLN.Set!CUSTOMER(V.Global.sCust)
	F.ODBC.conx!rstGLN.Set!SHIPTO(V.Global.sShipTo)
F.Intrinsic.Control.EndIf
F.ODBC.conx!rstGLN.Set!GLN(V.Screen.F_GLN!txtGLN.Text.rtrim)
F.ODBC.conx!rstGLN.Update

F.ODBC.conx!rstGLN.Close

f.Intrinsic.Control.If(V.Screen.F_GLN!txtGLN.Text.rtrim,=,"")
	V.Passed.000202.Set("Enter GLN")
F.Intrinsic.Control.Else
	V.Passed.000202.Set("GLN")
F.Intrinsic.Control.EndIf

F.Intrinsic.Control.CallSub(Unload)

program.sub.cmdsave_click.end

Program.Sub.Comments.Start
${$0$}$ATG_3732_GLN$}$JCT$}$4/8/2016
${$1$}$$}$$}$3$}$14430$}$ARS002S-SCRIPT-2-HOOK$}$4/8/2016 8:35:07 AM$}$(Program: ORD064GM; Screen: ARS002S)

${$1$}$$}$$}$2$}$14410$}$ARS002S-POPULATE-HOOK$}$4/8/2016 8:34:51 AM$}$(Program: ORD064GM; Screen: ARS002S)

${$1$}$$}$$}$1$}$14370$}$Script 2$}$4/8/2016 8:34:37 AM$}$(Program: AR0020GI; Screen: ARS002A1)

${$1$}$$}$$}$0$}$14350$}$Populate Hook$}$4/8/2016 8:34:19 AM$}$(Program: AR0020GI; Screen: ARS002A1)

Program.Sub.Comments.End

